# vi: ft=sh
# [doc](https://just.systems/man/zh/)
# [grammar](https://github.com/casey/just/blob/master/GRAMMAR.md)
# [cheetsheet](https://cheatography.com/linux-china/cheat-sheets/justfile/)

kPWD := justfile_directory()

default:
    @just --justfile '{{justfile()}}' --list

var:
    @just --justfile '{{justfile()}}' --evaluate

bin-colorized-logs:
    #!/usr/bin/env bash
    set -euxo pipefail
    _REPO="https://github.com/kilobyte/colorized-logs.git"
    _PWD="share/colorized-logs"
    _PREFIX="${HOME}/.local"
    _BIN="${_PREFIX}/bin/ansi2txt"  # for check has been installed
    if [ ! -e "$_BIN" ]; then
        if [ ! -e "$_PWD" ]; then
            git clone --depth 1 "$_REPO" "$_PWD"
        fi
        cd "$_PWD"
        [[ -e "build" ]] && rm -rf "build"
        mkdir "build" && pushd "build"
        cmake -DCMAKE_INSTALL_PREFIX:PATH="$_PREFIX" .. && make && make install
        popd && rm -rf "build"
    fi

bin-authbind:
    #!/usr/bin/env bash
    set -euxo pipefail
    # if which authbind > /dev/null; then
    #     # authbind installed, exit success
    #     exit 0
    # fi
    if [[ $(uname) == "Darwin" ]]; then
        _REPO="https://github.com/Castaglia/MacOSX-authbind.git"
        _PWD="share/authbind"
        if [ -e "$_PWD" ]; then rm -rf "$_PWD"; fi
        git clone --depth 1 "$_REPO" "$_PWD"
        pushd "$_PWD"
        if [[ $(uname -m) == "arm64" ]]; then
            sed -i '' 's/^ARCH=-arch.*$/ARCH=-arch arm64/g' Makefile
        fi
        make && sudo make install
        popd && rm -rf "$_PWD"
    else
        # TODO
        false
    fi

bin-autoadb:
    #!/usr/bin/env bash
    set -euxo pipefail
    _REPO="https://github.com/rom1v/autoadb.git"
    _PWD="share/autoadb"
    _BIN="${HOME}/.local/bin/autoadb"
    if [ ! -e "$_BIN" ]; then
        if [ ! -e "$_PWD" ]; then
            git clone --depth 1 "$_REPO" "$_PWD"
        fi
        cd "$_PWD"
        cargo build --release
        mv "target/release/autoadb" "$_BIN"
    fi

