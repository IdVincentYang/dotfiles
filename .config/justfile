# vi: ft=just tabstop=4 shiftwidth=4 expandtab

set shell := ["bash", "-eu", "-o", "pipefail", "-c"]

# 命名约定
#   <主分类>-<功能领域>-<name>[-<os>]
#   - 主分类(primary): core/system/cli/gui/extra/build
#   - 功能领域(domain): develop/media/productivity/db/vm/util/other
#   - 可选 OS 后缀：darwin/linux/termux（缺省表示通用）
# 新增 recipe 必须遵守上述命名，方可被 `list`/`install` 等命令识别。
primary_categories := "core system cli gui extra build"
domain_categories := "develop media productivity db vm util other"
asdf_plugins := "nodejs java direnv"
npm_global_packages := "@google/gemini-cli @openai/codex @upstash/context7-mcp repomix"

default:
    @just --list

help:
    @printf '%s\n' \
    '命令概览:' \
    '  just list [main=core] [domain=develop] [os=darwin] [name=git] [raw=1]' \
    '      - 根据命名规范(<主类>-<领域>-<名称>[-<平台>])筛选可用 recipe。' \
    '  just install <name> [name...]' \
    '      - 根据名称匹配当前平台的 recipe，直接执行安装。' \
    '  just install-menu [main=core] [domain=develop] [os=linux]' \
    '      - 交互式多选安装，支持 fzf (若已安装)。' \
    '  just platform-info' \
    '      - 显示平台判定结果，便于调试。' \
    '' \
    '命名规范: <主分类>-<领域>-<name>[-<os>]，例如 core-develop-git-darwin。'

platform-info:
    @just/platform.sh

list *filters:
    @JUSTFILE_PATH="{{justfile()}}" just/list.sh {{filters}}

install *packages:
    @JUSTFILE_PATH="{{justfile()}}" TARGET_PLATFORM="$(just/platform.sh)" just/install.sh {{packages}}

install-menu *filters:
    @JUSTFILE_PATH="{{justfile()}}" TARGET_PLATFORM="$(just/platform.sh)" just/install_menu.sh {{filters}}

# --- core: base toolchain & CLI essentials ---

# 基础构建依赖
core-develop-autoconf:
    brew install autoconf

core-develop-automake:
    brew install automake

core-util-p7zip:
    brew install p7zip

core-util-tree:
    brew install tree

core-productivity-zoxide:
    brew install zoxide

### fonts
#   nerd fonts(https://github.com/ryanoasis/nerd-fonts/blob/master/readme_cn.md)
#       config terminal: menu->setting...->text->font->change font
core-media-jetbrains-mono-nerd-font:
    brew install font-jetbrains-mono-nerd-font

# asdf 安装流程
core-develop-asdf:
    brew install asdf

core-develop-asdf-tool-versions:
    ln -snf "./asdf/tool-versions" "${HOME}/.tool-versions"

core-develop-asdf-install:
    asdf install

core-develop-asdf-plugins:
    #!/usr/bin/env bash
    set -euo pipefail
    # Ensure common language/tool plugins exist for asdf.
    plugins=({{asdf_plugins}})
    existing="$(asdf plugin list 2>/dev/null || true)"
    for plugin in "${plugins[@]}"; do
        if printf '%s\n' "${existing}" | grep -qx "${plugin}"; then
            printf 'asdf plugin already present: %s\n' "${plugin}"
        else
            asdf plugin add "${plugin}"
        fi
    done

core-develop-asdf-install-menu:
    ASDF_TARGET_PLUGINS="{{asdf_plugins}}" just/asdf_install_menu.sh

# 通用 CLI 套件
core-cli-bat:
    # bat -> cat: Clone of cat(1) with syntax highlighting and Git integration.
    brew install bat

core-cli-btop:
    # btop -> top: Resource monitor. Enhanced top command.
    brew install btop

core-cli-fd:
    # fd -> find: Simple, fast and user-friendly alternative to find.
    brew install fd

core-cli-fzf:
    # fzf: Command-line fuzzy finder.
    brew install fzf

core-cli-ripgrep:
    # ripgrep -> find: Search tool like grep.
    brew install ripgrep

core-cli-tealdeer:
    # tealdeer -> man: short TLDR client.
    brew install tealdeer

core-cli-npm-globals:
    #!/usr/bin/env bash
    set -euo pipefail
    if ! command -v npm >/dev/null 2>&1; then
        echo "[npm-globals] npm 未安装，请先运行 core-develop-asdf 或相关 node 安装流程" >&2
        exit 1
    fi
    packages=({{npm_global_packages}})
    for pkg in "${packages[@]}"; do
        echo "[npm-globals] 安装/更新 ${pkg}"
        npm install -g "$pkg"
    done

# 网络/同步工具
# core-util-adwaita-icon-theme:
#     # 仅用于 macOS 上的 GTK 应用（例如 grsync）补齐主题；等待 grsync 迁移时再一起处理。

core-cli-grsync:
    brew install grsync

core-cli-rsync:
    brew install rsync

core-cli-wget:
    brew install wget

core-media-yt-dlp:
    brew install yt-dlp

# --- system: desktop integration & utilities ---

### hammerspoon & tray helpers
system-util-hammerspoon-darwin:
    brew install --cask --no-quarantine hammerspoon
    defaults write org.hammerspoon.Hammerspoon MJConfigFile "${XDG_CONFIG_HOME:-$HOME/.config}"/hammerspoon/init.lua

system-util-caffeine-darwin:
    # tray: easily block OS from sleeping
    brew install --cask caffeine

system-productivity-easydict-darwin:
    # tray: easydict translator
    brew install --cask easydict

system-productivity-dropshelf-darwin:
    # tray: drag & drop shelf
    brew install --cask dropshelf

system-productivity-neardrop-darwin:
    # tray: nearby Android sharing
    brew install --cask grishka/grishka/neardrop

system-util-stats-darwin:
    # tray: system monitor utility
    brew install --cask stats

### system applications
system-media-quickrecorder-darwin:
    brew install lihaoyun6/tap/quickrecorder

system-productivity-raycast-darwin:
    # spotlight replacement
    brew install --cask raycast

system-util-mediainfo-darwin:
    # Finder contextual media info
    brew install --cask mediainfo

system-productivity-openvanilla-darwin:
    brew install openvanilla
    mkdir -p "${HOME}/Library/Application Support/OpenVanilla/UserData/TableBased/erbi.cin"
    ln -snf "./backups/erbi/OpenVanilla/erbi.cin" "${HOME}/Library/Application Support/OpenVanilla/UserData/TableBased/erbi.cin"

system-media-aerial-darwin:
    brew install --cask aerial

### Quick Look enhancements
system-media-quicklook-suite-darwin:
    brew install --cask apparency suspicious-package
    brew install --cask --no-quarantine qladdict qlcolorcode qlprettypatch qlvideo quicklook-json syntax-highlight
    xattr -cr ~/Library/QuickLook/QLAddict.qlgenerator
    xattr -cr ~/Library/QuickLook/qlprettypatch.qlgenerator
    qlmanage -r cache
    open /Applications/QuickLook\ Video.app

### system tweaks
system-util-karabiner-elements-darwin:
    brew install --cask karabiner-elements

system-productivity-keepassxc-darwin:
    brew install --cask keepassxc

system-util-macvim-darwin:
    brew install --cask macvim

system-util-kitty-darwin:
    brew install --cask kitty

system-util-icefloor-darwin:
    # Firewall / pf 可视化管理
    brew install --cask icefloor

system-other-scrcpy:
    brew install scrcpy

### system database services
system-db-postgresql16:
    # sql server and client app
    # - default db named `postgresql` created under     /opt/homebrew/var/postgresql@16
    brew install postgresql@16
    brew services start postgresql@16

# --- cli: developer tooling & productivity ---

### AI / command line helpers
cli-develop-aichat:
    # aichat CLI; functions repo可后续通过 yadm submodule 管理。
    # submodule init in yadm bootstrap stage: yadm submodule update --init
    brew install aichat

### Git utilities
cli-develop-git-archive-all:
    # Archive repository including submodules.
    brew install git-archive-all

cli-develop-onefetch:
    # Git repo summary in terminal.
    brew install onefetch

gui-develop-fork-darwin:
    # Git GUI client (Fork)。
    brew install --cask fork

### Languages & build tools
cli-develop-rustup:
    brew install rustup
    rustup default stable

cli-develop-tokei:
    # Count source lines of code.
    brew install tokei

cli-develop-cmake:
    brew install cmake

gui-develop-beyond-compare-darwin:
    # File diff/merge tool。若需重置试用：删除 ~/Library/Application Support/Beyond Compare/registry.dat
    brew install --cask beyond-compare

gui-develop-visual-studio-code:
    brew install --cask visual-studio-code

### Network / debugging suite
cli-develop-iperf3:
    brew install iperf3

cli-develop-iproute2mac-darwin:
    brew install iproute2mac

gui-develop-sloth-darwin:
    brew install --cask sloth

gui-develop-charles-darwin:
    brew install --cask charles

gui-develop-wireshark:
    brew install --cask wireshark

gui-develop-postman:
    brew install --cask postman

# --- gui: desktop applications ---

## 下载/网络工具
gui-productivity-cyberduck-darwin:
    brew install --cask cyberduck

gui-other-aria2gui-darwin:
    # aria2 GUI front-end
    brew install --cask aria2gui

## 浏览器
gui-productivity-google-chrome:
    brew install --cask google-chrome

## 多媒体
gui-media-gimp:
    brew install --cask gimp

gui-media-inkscape:
    brew install --cask inkscape

gui-media-losslesscut:
    brew install --cask losslesscut

gui-media-invisor-lite-darwin:
    brew install --cask invisor-lite

gui-media-iina-darwin:
    brew install --cask iina
    mkdir -p "${HOME}/Library/Application Support/com.colliderli.iina/input_conf"
    ln -snf "${HOME}/.config/backups/iina/input_conf/IINA_MY.conf" "${HOME}/Library/Application Support/com.colliderli.iina/input_conf/IINA_MY.conf"

## 字体/外设
# (intentionally left blank for future groupings)

## 游戏/虚拟化
gui-vm-xemu-darwin:
    brew install --cask xemu

gui-vm-vmware-fusion-darwin:
    # vm ware, replacing older solutions
    # brew install --cask rdm virtualbox virtualbox-extension-pac
    # brew install wine-stable
    brew install --cask vmware-fusion

## 数据库客户端
gui-db-db-browser-for-sqlite-darwin:
    # sqlite client app
    brew install --cask --no-quarantine db-browser-for-sqlite

gui-db-pgadmin4-darwin:
    # PostgreSQL GUI 管理工具
    brew install --cask --no-quarantine pgadmin4

## 开发辅助
extra-productivity-applite-darwin:
    # app: homebrew GUI
    # brew install cakebrew
    # app: homebrew GUI for cask apps
    brew install --cask --no-quarantine applite

# --- build: source-based installs ---

build-util-colorized-logs:
    #!/usr/bin/env bash
    set -euxo pipefail
    repo="https://github.com/kilobyte/colorized-logs.git"
    workdir="share/colorized-logs"
    prefix="${HOME}/.local"
    bin="${prefix}/bin/ansi2txt"
    if [[ ! -e "$bin" ]]; then
        if [[ ! -d "$workdir" ]]; then
            git clone --depth 1 "$repo" "$workdir"
        fi
        cd "$workdir"
        [[ -d build ]] && rm -rf build
        mkdir build
        pushd build >/dev/null
        cmake -DCMAKE_INSTALL_PREFIX:PATH="$prefix" ..
        make
        make install
        popd >/dev/null
        rm -rf build
    fi

build-util-authbind-darwin:
    #!/usr/bin/env bash
    set -euxo pipefail
    repo="https://github.com/Castaglia/MacOSX-authbind.git"
    workdir="share/authbind"
    rm -rf "$workdir"
    git clone --depth 1 "$repo" "$workdir"
    pushd "$workdir" >/dev/null
    if [[ "$(uname -m)" == "arm64" ]]; then
        sed -i '' 's/^ARCH=-arch.*$/ARCH=-arch arm64/g' Makefile
    fi
    make
    sudo make install
    popd >/dev/null
    rm -rf "$workdir"

build-develop-autoadb:
    #!/usr/bin/env bash
    set -euxo pipefail
    repo="https://github.com/rom1v/autoadb.git"
    workdir="share/autoadb"
    bin="${HOME}/.local/bin/autoadb"
    if [[ ! -d "$workdir" ]]; then
        git clone --depth 1 "$repo" "$workdir"
    fi
    if [[ ! -e "$bin" ]]; then
        cd "$workdir"
        cargo build --release
        install -m 755 target/release/autoadb "$bin"
    fi
