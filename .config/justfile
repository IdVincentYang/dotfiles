# vi: ft=sh

set shell := ["bash", "-eu", "-o", "pipefail", "-c"]

primary_categories := "core system cli gui extra"
domain_categories := "develop media productivity db vm util other"

default:
    @just --list

help:
	@printf '%s\n' \
	    '命令概览:' \
	    '  just list [main=core] [domain=develop] [os=darwin] [name=git] [raw=1]' \
	    '      - 根据命名规范(<主类>-<领域>-<名称>[-<平台>])筛选可用 recipe。' \
	    '  just install <name> [name...]' \
	    '      - 根据名称匹配当前平台的 recipe，直接执行安装。' \
	    '  just install-menu [main=core] [domain=develop] [os=linux]' \
	    '      - 交互式多选安装，支持 fzf (若已安装)。' \
	    '  just platform-info' \
	    '      - 显示平台判定结果，便于调试。' \
	    '' \
	    '命名规范: <主分类>-<领域>-<name>[-<os>]，例如 core-develop-git-darwin。'

platform-info:
	@just/platform.sh

list main="" domain="" os="" name="" raw="0":
	@JUSTFILE_PATH="{{justfile()}}" just/list.sh "{{main}}" "{{domain}}" "{{os}}" "{{name}}" "{{raw}}"

install +packages:
	@JUSTFILE_PATH="{{justfile()}}" TARGET_PLATFORM="$(just/platform.sh)" just/install.sh {{packages}}

install-menu main="" domain="" os="":
	@JUSTFILE_PATH="{{justfile()}}" TARGET_PLATFORM="$(just/platform.sh)" just/install_menu.sh "{{main}}" "{{domain}}" "{{os}}"
